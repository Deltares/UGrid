project(
  UGridNETTests
  VERSION ${CMAKE_PROJECT_VERSION}
  DESCRIPTION "UGrid C# API tests"
  LANGUAGES CSharp
)

# target name
set(TARGET_NAME ${PROJECT_NAME})

# set(ASSEMBLY_INFO_CS ${CMAKE_CURRENT_BINARY_DIR}/Properties/AssemblyInfo.cs)
# configure_file(
#   ${CMAKE_CURRENT_SOURCE_DIR}/../cs_proj_templates/AssemblyInfo.cs.in
#   ${ASSEMBLY_INFO_CS}
#   @ONLY
# )

set(APP_CONFIG ${CMAKE_CURRENT_BINARY_DIR}/App.config)
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/../cs_proj_templates/App.config.in
  ${APP_CONFIG}
  @ONLY
)

set(PACKAGE_REFERENCES
  "NUnit_4.2.0"
  "NUnit.ConsoleRunner_3.18.1"
  "NUnit3TestAdapter_4.6.0"
)

set(PACKAGES_CONFIG ${CMAKE_CURRENT_BINARY_DIR}/packages.config)

include(${CMAKE_SOURCE_DIR}/cmake/configure_dotnet_packages.cmake)
configure_dotnet_packages(
  ${PACKAGES_CONFIG}
  "${PACKAGE_REFERENCES}"
  ${GLOB_DOTNET_TARGET_FRAMEWORK}
)

add_library(${TARGET_NAME} 
  SHARED
  #${ASSEMBLY_INFO_CS}
  ${APP_CONFIG}
  ${PACKAGES_CONFIG}
  src/UGridNETTests.cs
)

target_link_libraries(${TARGET_NAME} PRIVATE UGridNET)

set(NUGET_DOWNLOAD_DIR ${TOOLS_INSTALL_DIR}/nuget)
file(MAKE_DIRECTORY ${NUGET_DOWNLOAD_DIR})
set(NUGET_EXE ${NUGET_DOWNLOAD_DIR}/nuget.exe)
if(NOT EXISTS ${NUGET_EXE})
  set(NUGET_URL "https://dist.nuget.org/win-x86-commandline/latest/nuget.exe")
  file(DOWNLOAD ${NUGET_URL} ${NUGET_EXE})
endif()

execute_process(
  COMMAND ${NUGET_EXE} restore packages.config
    -SolutionDirectory ${CMAKE_CURRENT_BINARY_DIR}
    -Force
    -NoHttpCache
    -Verbosity detailed
    -Source https://api.nuget.org/v3/index.json
  WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  RESULT_VARIABLE NUGET_RESULT
  ERROR_VARIABLE NUGET_ERROR
  OUTPUT_VARIABLE NUGET_OUTPUT
  ECHO_OUTPUT_VARIABLE
  ECHO_ERROR_VARIABLE
  COMMAND_ECHO STDOUT
  COMMAND_ERROR_IS_FATAL LAST
)

set_target_properties(
  ${TARGET_NAME}
  PROPERTIES
    DOTNET_TARGET_FRAMEWORK ${GLOB_DOTNET_TARGET_FRAMEWORK}
    DOTNET_TARGET_FRAMEWORK_VERSION ${GLOB_DOTNET_TARGET_FRAMEWORK_VERSION}
    DOTNET_SDK ${GLOB_DOTNET_SDK}
    VS_DOTNET_REFERENCES "System;System.Runtime.CompilerServices.Unsafe"
    VS_PACKAGE_REFERENCES "${PACKAGE_REFERENCES}"
)
